#!/usr/bin/env perl

#
# This configure script just checks that all the right dependencies are
# available. It does not (yet) generate any Makefiles or header files...
#

use strict;
use File::Temp qw(tempfile tempdir);
use Getopt::Long;

exit 1 unless GetOptions "with-gc-dir=s" => \my $gc_dir,
                         "with-gmp-dir=s" => \my $gmp_dir;
my $CFLAGS = "";
if($gc_dir) {
    $CFLAGS .= " -I$gc_dir/include -L$gc_dir/lib";
}
if($gmp_dir) {
    $CFLAGS .= " -I$gmp_dir/include -L$gmp_dir/lib";
}

sub compile {
    my ($flags, $src) = @_;
    my ($fh, $filename) = tempfile SUFFIX => ".c";
    my $binary = $filename;
    $binary =~ s/\.c$//;
    print $fh $src;
    close $fh;
    my $output = `$ENV{CC} $CFLAGS $flags -o $binary $filename 2>&1`;
    chomp $output;
    $? == 0 && !$output;
};

sub check_lib {
    my ($lib, $src) = @_;
    print "Checking for lib$lib... ";
    if(compile "-l$lib", $src) {
        print "ok\n";
    } else {
        print "failed\n";
        print "\n\e[31mCould not find lib$lib (used -l$lib)\nPlease make sure it is available before building Slash\n\e[0m";
        exit 1;
    }
};

sub check_prog {
    my ($cmd, $expect) = @_;
    my ($prog) = split / /, $cmd;
    print "Checking for $prog... ";
    ($_) = `$cmd 2>&1`;
    chomp;
    if(/$expect/) {
        print "$_\n";
    } else {
        print "failed\n";
        print "\n\e[31mCould not find $prog (used $cmd)\nPlease make sure it is available before building Slash\n\e[0m";
        exit 1;
    }
}

if(!defined $ENV{CC}) {
    print "\e[33mCC environment variable not set, falling back to 'cc'...\n\e[0m";
    $ENV{CC} = "cc";
}
check_prog "$ENV{CC} --version";

check_prog "flex --version", /flex 2/;

check_lib "gmp", <<C;
#include <gmp.h>

int main() {
    mpz_t mpz;
    mpz_init(mpz);
}
C

check_lib "gc", <<C;
#include <gc.h>

int main() {
    GC_INIT();
}
C

open my $mf, ">local.mk";
print $mf "CFLAGS+= $CFLAGS\n";

my %platforms = (
    darwin  => sub { print $mf "OBJS+= platform/posix.o platform/darwin.o\n"; },
    linux   => sub { print $mf "OBJS+= platform/posix.o platform/linux.o\n"; },
);
if($platforms{$^O}) {
    $platforms{$^O}->();
} else {    
    print "\n\e[31mUnknown system $^O. Please file an issue at https://github.com/haileysome/slash/issues.\n\e[0m";
    exit 1;
}

close $mf;

print "\n\e[32mReady to build\n\e[0m";